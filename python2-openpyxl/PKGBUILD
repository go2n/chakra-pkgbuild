# Maintainer: Sarwo Hadi Setyana <sh.setyana@gmail.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Nishit Joseph (reachjlight at gmail dot com)
# Contributor: Martin Corley <Martin.Corley@ed.ac.uk>

pkgname=python2-openpyxl
pkgver=2.2.4
pkgrel=1
pkgdesc="A Python library to read/write Excel 2007 xlsx/xlsm files"
arch=('any')
url="http://openpyxl.readthedocs.org/"
license=('MIT')
makedepends=('python2-setuptools' 'python2-jdcal' 'python2-lxml')
source=("https://pypi.python.org/packages/source/o/openpyxl/openpyxl-${pkgver}.tar.gz"
        "LICENCE")
md5sums=('c18efe86fc96bbb9f62e2a4ed64ef430'
         'c8afe530744e932b892358e6eb5bea9b')

prepare() {
  cd "$srcdir"
  cd openpyxl-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building"
  cd "$srcdir"/openpyxl-${pkgver}
  python2 setup.py build
}

package() {
  depends=('python2' 'python2-jdcal' 'python2-lxml')
  optdepends=('python2-pillow: needed to include images')

  cd "$srcdir"/openpyxl-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir"/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
